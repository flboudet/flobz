import <flying_flobo.gsl>

int @inNetGameCenter
int @isFullScreen
float @kaoriMovesFCycle
float @kaoriDestX

<init>
  float @BigPuyoDelay = 200.
  float @BigPuyoShowAfter = @BigPuyoDelay
  int   @ShowBigPuyo = 0
  int   @BigPuyoApogee = 0
  float @ShowBigPuyoInitialCycle = 0.

  # Init function: called when script is loaded
  Display @display  = [new_display: x=0.0 y=0.0 width=640.0 height=480.0]

  Sprite @kaori = [new_sprite: image="kaori_title/kaori_title.png" x = -300.0 y=70.0 curframe=0.0 framespeed=0.15 nbframes=2.0]
  Sprite @kaorieyes = [new_sprite: image="kaori_title/kaori_title_eyesclosed.png" x = 92.0 y=83.0 curframe=0.0 framespeed=0.4 nbframes=40.0]
  @kaorieyes.parent = "@kaori"
  Sprite @bigpuyo = [new_sprite: image="happypuyo.png" x = 500.0 y=480.0 curframe=0.0 framespeed=0.15 nbframes=1.0]
  @isFullScreen = 1
  flying_flobo_init
  Sprite @appStore = [new_sprite: image="app_store.png" x=20.0 y=490.0 curframe=0.0  framespeed=0.15 nbframes=1.0]

  # Cache resources for future use
#import <cache.gsl>

<sprite_list>
  # Call the 'sprite' function for each sprite you want to update/draw
  # This also allows you to specify the drawing order
  (@isFullScreen = 0)? {
    flying_flobo_sprite_list
  }
  sprite::@kaori display=@display
  sprite::@kaorieyes display=@display
  sprite::@bigpuyo display=@display
  sprite::@appStore display=@display

<update>
  # Update function: called every animation cycle

  @kaori.image = "kaori_title/kaori_title.png"
  (@kaori.curframe > 1.0)? @kaori.image = "kaori_title/kaori_title2.png"

  @kaorieyes.image = "null.png"
  (@kaorieyes.curframe > 39.0)? @kaorieyes.image = "kaori_title/kaori_title_eyesclosed.png"

  flying_flobo_update

  (@BigPuyoShowAfter <= @fcycle)? {
    @ShowBigPuyo = 1
    @BigPuyoApogee = 0
    @ShowBigPuyoInitialCycle = @fcycle
    @BigPuyoShowAfter = @BigPuyoShowAfter + @BigPuyoDelay
  }
  (@ShowBigPuyo = 1)? {
    @bigpuyo.pos.y = 500.0 - ([fabs: x=([sin: value = ((@fcycle - @ShowBigPuyoInitialCycle)/10.0)] * 300.0)])
    (@bigpuyo.pos.y < 400.)? @BigPuyoApogee = 1
    (@BigPuyoApogee = 1)? (@bigpuyo.pos.y > 480.)? @ShowBigPuyo=0
  }
  (@inNetGameCenter = 0)? {
    (@isFullScreen = 1)? {
      @kaoriMovesFCycle = @fcycle + 10.0
      @kaoriDestX=20.0
      @isFullScreen = 0
    }
    music: command="menu"
  }
  (@inNetGameCenter = 1)? {
    (@isFullScreen = 0)? {
      @kaoriMovesFCycle = @fcycle + 20.0
      @kaoriDestX=-300.0
      @isFullScreen = 1
    }
    music: command="network menu"
  }
  move::@kaori   fcycle=@kaoriMovesFCycle      x=@kaoriDestX y=@kaori.pos.y
  move::@appStore fcycle=70.0 x=20.0 y=490.0
  move::@appStore fcycle=100.0 x=20.0 y=415.0
  move::@appStore fcycle=200.0 x=20.0 y=415.0
  move::@appStore fcycle=230.0 x=20.0 y=490.0
  (@cycle = 1000)? @finished = 1
